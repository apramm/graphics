<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 A4, Sep 2023</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
    <script src="js/three.min-r108.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- http://jetienne.com/ -->

<script id="myVertexShader" type="x-shader/x-vertex">

precision highp float;

////////////////////
// NAME:  Apram Ahuja, apri   ADD YOUR NAME and CWL login here
// COMMENTS:  extra comments go here
//  I changed the x-coordinates such that it doesn't overlap with each other
//////////////////  

////////// UNCOMMENT THE FOLLOWING WHEN USING WITH   https://shdr.bkcore.com/
// attribute vec3 position;
// attribute vec3 normal;
// uniform mat3 normalMatrix;
// uniform mat4 modelViewMatrix;
// uniform mat4 projectionMatrix;

varying vec3 varyingNormal;
void main()
{
        varyingNormal = normal;
	vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
	gl_Position = projectionMatrix * mvPosition;
}
</script>

<script id="myFragmentShader" type="x-shader/x-fragment">

  precision highp float;

  //////////////////////////////////////////////////
  // scaledImplicitABC()
  // input:  
  //   - two points, p1 and p2, that the line should pass through, having F(x,y)=0
  //   - a third point, p3,  where we want F(x,y)=1
  // output:  A,B,C,  corresponding to   F(x,y) = A*x + B*y + C   
  //////////////////////////////////////////////////
  
  void scaledImplicitABC(vec2 p1, vec2 p2, vec2 p3, out float A, out float B, out float C) {
    A = p1.y - p2.y;
    B = p2.x - p1.x;
    C = p1.x * p2.y - p2.x * p1.y;
  
    
    float scale = A * p3.x + B * p3.y + C;
        A /= scale;
        B /= scale;
        C /= scale;
  }
  
  
  ///////////////////////////////////////////
  //  getBarycentric():   input is triangle and (x,y) query point; 
  //                      output is the barycentric coordinates
  ///////////////////////////////////////////
  
  void getBarycentric(vec2 p1, vec2 p2, vec2 p3, float x, float y, 
    out float alpha, out float beta, out float gamma) {
    
    float A12, B12, C12;    // implicit line parameters for edge 12
    float A23, B23, C23;    // implicit line parameters for edge 23
    float A13, B13, C13;    // implicit line parameters for edge 13
  
    scaledImplicitABC(p1,p2,p3,A12,B12,C12);
    gamma = A12*x + B12*y + C12;
    
    scaledImplicitABC(p2,p3,p1, A23, B23, C23);
    alpha = A23*x + B23*y + C23;
    
    scaledImplicitABC(p1,p3,p2, A13, B13, C13);
    beta = A13*x + B13*y + C13;
  }
  
  ///////////////////////////////////////////
  //  insideSquare()
  ///////////////////////////////////////////
  
  void insideSquare(float x, float y) {
      float xmin = 300.0;
      float ymin = 300.0;
      float size = 200.0;
  
      
      float F_left = x - xmin; 
      float F_right = (xmin + size) - x; 
      float F_bottom = y - ymin; 
      float F_top = (ymin + size) - y;
  
      
      if (F_left > 0.0 && F_right > 0.0 && F_bottom > 0.0 &&            F_top > 0.0) {
  
          
          gl_FragColor = vec4(1, 1, 1, 1);
      }
  }
  
  
  ///////////////////////////////////////////
  //  insideCircle()
  ///////////////////////////////////////////
  
  void insideCircle(float x, float y) {
  
      vec2 center = vec2(700, 400);
      float r1 = 40.0; 
      float r2 = 120.0; 
  
      float F1 = length(vec2(x, y) - center) - r1;
      float F2 = length(vec2(x, y) - center) - r2;
  
      if (F1 > 0.0 && F2 < 0.0) {
          gl_FragColor = vec4(1, 1, 1, 1);
      }
  }
  
  
  ///////////////////////////////////////////
  //  insideSphere()
  ///////////////////////////////////////////
  
  void insideSphere(float x, float y) {
      float xc = 300.0;
      float yc = 150.0;
      float rad = 100.0;
  
  
      vec3 L = vec3(10, 10, 10);
      vec3 N;
  
      float rx = x - xc;
      float ry = y - yc;
  
      float F = length(vec2(rx, ry)) - rad;
  
      if (F < 0.0) { 
  
          float rz = sqrt(rad * rad - rx * rx - ry * ry);
          N = normalize(vec3(rx, ry, rz));
  
          
          L = normalize(L);
  
          
          float intensity = dot(N, L);
          float i = clamp(intensity, 0.0, 1.0);
  
          
          gl_FragColor = vec4(i, i, i, 1.0);
      }
  }
  
  
    
  ///////////////////////////////////////////
  // textureMap(u,v)
  // Given (u,v) texture map coordinates, returns a procedurally-computed color
  // This would often come from an image.
  ///////////////////////////////////////////
  
  vec3 textureMap(vec2 uv) {
      float u = clamp(uv[0],0.0,1.0)*3.0;
      float v = clamp(uv[1],0.0,1.0)*3.0;
      float dx = u - floor(u) - 0.5;
      float dy = v - floor(v) - 0.5;
      float r = sqrt(dx*dx + dy*dy);
      int i = int(u);
      int j = int(v);
      if (r>0.5)
    return vec3(1,1,1);  // outside circles is white
      else if (i==0 && j==0)   // circle closest to (0,0): red
    return vec3(1,0,0);
      else if (i==2 && j==0)   // circle closest to (1,0): green
    return vec3(0,1,0);
      else
    return vec3(0,0,1);  // other circles:  blue
  }
    
  ///////////////////////////////////////////
  //  insideTriangleBasic():  solid-shaded triangle
  ///////////////////////////////////////////
  
  void insideTriangleBasic(vec2 P1, vec2 P2, vec2 P3, float x, float y) {
      float alpha, beta, gamma;
  
      getBarycentric(P1, P2, P3, x, y, alpha, beta, gamma);
  
      if (alpha >= 0.0 && beta >= 0.0 && gamma >= 0.0) {
          
        
          gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
      }
  }
  
  
  ///////////////////////////////////////////
  //  insideTriangleAlpha():  shade with value of alpha
  ///////////////////////////////////////////
  
  void insideTriangleAlpha(vec2 P1, vec2 P2, vec2 P3, float x, float y) {
      float alpha, beta, gamma;
  
      getBarycentric(P1, P2, P3, x, y, alpha, beta, gamma);
  
      
      if (alpha >= 0.0 && beta >= 0.0 && gamma >= 0.0) {
          
          gl_FragColor = vec4(alpha, alpha, alpha, 1.0);
  
  
      }
  
  }
  
  
  ///////////////////////////////////////////
  //  insideTriangleColor():  shade with color interpolation
  ///////////////////////////////////////////
  
  void insideTriangleColor(vec2 P1, vec2 P2, vec2 P3, vec3 C1, vec3 C2, vec3 C3, float x, float y) {
      float alpha, beta, gamma;
  
      getBarycentric(P1, P2, P3, x, y, alpha, beta, gamma);
  
    
      if (alpha >= 0.0 && beta >= 0.0 && gamma >= 0.0) {
          
          vec3 interpolatedColor = alpha * C1 + beta * C2 +              gamma * C3;
          
          
          gl_FragColor = vec4(interpolatedColor, 1.0);
      }
      
  }
  
  
  ///////////////////////////////////////////
  //  insideTriangleTextureMap():  texture-mapped triangle
  ///////////////////////////////////////////
  
  void insideTriangleTextureMap(vec2 P1, vec2 P2, vec2 P3, vec2 uv1, vec2 uv2, vec2 uv3, float x, float y) {
      float alpha, beta, gamma;
  
      getBarycentric(P1, P2, P3, x, y, alpha, beta, gamma); // compute the barycentric coords for P(x,y)
  
      if (alpha >= 0.0 && beta >= 0.0 && gamma >= 0.0) {
          
          vec2 interpolatedUV = alpha * uv1 + beta * uv2 + gamma * uv3;
          vec3 color = textureMap(interpolatedUV);
  
          
          gl_FragColor = vec4(color, 1.0);
      }
      
  }
  
  
  ///////////////////////////////////////////
  //  main()
  ///////////////////////////////////////////
  
  void main( void ) {
  
      // default color;  note that this can be overwritten by
      // gl_FragColor assignments in the functions below
    gl_FragColor = vec4(0,0.6,0.6,1.0);  // default fragment color
    float x = gl_FragCoord.x;    // pixel x coordinate in DCS
    float y = gl_FragCoord.y;    // pixel y coordinate in DCS
  
    insideSquare(x,y);
    insideCircle(x,y);
    insideSphere(x,y);
  
    vec2 P1 = vec2(400,100);
    vec2 P2 = vec2(500,100);
    vec2 P3 = vec2(450,250);
    insideTriangleBasic(P1,P2,P3,x,y);
  
    P1.x += 100.0; P2.x += 100.0;  P3.x += 100.0;
    insideTriangleAlpha(P1,P2,P3,x,y);
      
    P1.x += 100.0; P2.x += 100.0;  P3.x += 100.0;
    vec3 C1 = vec3(1,0,0), C2 = vec3(0,1,0), C3 = vec3(0,0,1);
    insideTriangleColor(P1,P2,P3,C1,C2,C3,x,y);
  
    P1 = vec2(700,200);
    P2 = vec2(875,100);
    P3 = vec2(925,250);
    vec2 uv1 = vec2(0,0);
    vec2 uv2 = vec2(1,0);
    vec2 uv3 = vec2(1,1);
    insideTriangleTextureMap(P1,P2,P3,uv1,uv2,uv3,x,y);
  }
</script>


<script src="a4.js"></script>

</body>
</html>

